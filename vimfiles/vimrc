" vim:set foldmethod=marker:
if has('win32') || has('win64')
    set   termencoding=cp932
    " :PlugUpdate fails if `pwsh.exe` is set for shell...
    " https://www.reddit.com/r/neovim/comments/gbb2g3/wierd_vimplug_error_messages/g3n3vtl/
    "if executable('pwsh')
    "    set shell=pwsh.exe
    "endif
endif
if has('linux')
set   path+=/usr/include/**
endif

" File & Edit
set   backspace=indent,eol,start
set   encoding=utf-8
set   fileencoding=utf-8
set   fileencodings=ucs-bom,utf-8,cp932
set nobackup
set nowritebackup
set noswapfile
set noundofile
if has('unnamedplus')
    set clipboard&
    set clipboard^=unnamedplus
endif

" User Interface & Appearence
set notitle
set noruler
set nonumber
set   laststatus=2
set   statusline =%n\ %<%f%R%M,%{&ff}%y%h%w%q
set   statusline+=%=\ %(%l,%c%V%)\ %p%%
set   statusline+=\ %#warningmsg#
set   statusline+=%*
set   updatetime=500
set   wildmenu
set   mouse=
set   completeopt-=preview  " Suppress opening a scratch buffer on completion

" Search
set   hlsearch
set   ignorecase
set   smartcase
set nowrapscan

" Misc.
if executable('rg')
    set   grepprg=rg\ --vimgrep
endif
set  wildignore =*.swp,*.~*
set  wildignore+=*.o,*.obj
set  wildignore+=*.so,*.dll
set  wildignore+=*.py[cod]
set  wildignore+=*.min.*
autocmd BufWinEnter * match ExtraWhitespace /\s\+$/
highlight ExtraWhitespace ctermbg=red guibg=red
filetype indent plugin on

" Plugins
call plug#begin()
Plug 'nanotech/jellybeans.vim', { 'tag': '*' }
Plug 'editorconfig/editorconfig-vim', { 'tag': '*' }
Plug 'airblade/vim-gitgutter'
Plug 'kana/vim-submode',        { 'tag': '*' }
Plug 'justinmk/vim-sneak',      { 'tag': '*' }
Plug 'tpope/vim-commentary',    { 'tag': '*' }
Plug 'machakann/vim-sandwich',  { 'tag': '*' }
Plug 'tpope/vim-fugitive',      { 'tag': '*' }
Plug 'sheerun/vim-polyglot',    { 'tag': '*' }
Plug 'qpkorr/vim-renamer'
if has('nvim')
    Plug 'nvim-lua/plenary.nvim' " for telescope.nvim
    Plug 'kyazdani42/nvim-web-devicons' " for telescope.nvim

    Plug 'neovim/nvim-lspconfig'
    Plug 'williamboman/nvim-lsp-installer'
    Plug 'nvim-telescope/telescope.nvim'
    Plug 'nvim-lualine/lualine.nvim'
    Plug 'hrsh7th/nvim-cmp'
    Plug 'hrsh7th/cmp-nvim-lsp'
    Plug 'hrsh7th/cmp-buffer'
    Plug 'hrsh7th/cmp-path'
    Plug 'hrsh7th/cmp-cmdline'
    Plug 'onsails/lspkind-nvim'
else
    Plug 'junegunn/fzf',        { 'tag': '*', 'do': { -> fzf#install() } }
    Plug 'junegunn/fzf.vim'
endif
call plug#end()

syntax on

" Color scheme (note: load the plugin before this) {{{
set t_Co=256
if has('termguicolors')
    set termguicolors
endif

" Use jellybeans color scheme with a litle darker background
let g:jellybeans_overrides = { 'background' : { 'guibg': '0c0c0c' }, }
colorscheme jellybeans

" (git-gutter) Make markers for deleted line brighter
highlight GitGutterDelete       guifg=#ff000a guibg=#333333
highlight GitGutterChangeDelete guifg=#ff000a guibg=#333333
let g:gitgutter_sign_modified_removed = 'â‰…'
" }}} Color scheme

" Neovim specific configurations
if has('nvim')
    set omnifunc=v:lua.vim.lsp.omnifunc

lua << EOF
    local ok, lualine = pcall(require, "lualine")
    if ok then
        lualine.setup {}
    end

    local ok, lsp_installer = pcall(require, "nvim-lsp-installer")
    if ok then
        lsp_installer.on_server_ready(function(server)
            server:setup {}
            vim.cmd("do User LspAttachBuffers")
        end)
    end

    local ok, cmp = pcall(require, "cmp")
    if ok then
        lspkind = require "lspkind"
        cmp.setup {
            mapping = {
                ["<C-p>"]     = cmp.mapping.select_prev_item(),
                ["<C-n>"]     = cmp.mapping.select_next_item(),
                ["<C-d>"]     = cmp.mapping.scroll_docs(-4),
                ["<C-f>"]     = cmp.mapping.scroll_docs(4),
                ["<C-j>"]     = cmp.mapping.complete(),
                ["<C-e>"]     = cmp.mapping.close(),
                ["<CR>"]      = cmp.mapping.confirm({ behavior = cmp.ConfirmBehavior.Replace, select = true }),
                ["<C-y>"]     = cmp.mapping.confirm({ behavior = cmp.ConfirmBehavior.Replace, select = true }),
                ["<Tab>"]     = cmp.mapping.select_next_item({ behavior = cmp.SelectBehavior.Insert }),
                ["<S-Tab>"]   = cmp.mapping.select_prev_item({ behavior = cmp.SelectBehavior.Insert }),
            },
            sources = cmp.config.sources({
                { name = "nvim_lsp" },
            }, {
                { name = "buffer" },
                { name = "path" },
            }),
            formatting = {
                format = lspkind.cmp_format(),
            },
        }

        -- Complete VIM commands
        cmp.setup.cmdline(":", {
            sources = {
                { name = "cmdline" }
            }
        })

        -- Complete search words
        cmp.setup.cmdline("/", {
            sources = {
                { name = "cmdline" }
            }
        })

    end

    local ok, lsp_config = pcall(require, "lspconfig")
    if ok then
        lsp_config.efm.setup {
            init_options = {
                documentFormatting = true,
                hover = true,
                documentSymbol = true,
                codeAction = true,
                completion = true,
            },
            settings = {
                rootMarkers = {".git/"},
                languages = {
                    lua = {
                        {formatCommand = "lua-format -i", formatStdin = true}
                    },
                    python = {
                        {
                            lintCommand = "flake8 --stdin-display-name ${INPUT} -",
                            lintStdin = true,
                            --lintFormats = [ "%f:%l:%c: %m" ],
                            formatCommand = "black --quiet -",
                            formatStdin = true,
                        }
                    }
                }
            }
        }
    end

EOF
endif

" Keymaps {{{

" Code-completion (not for Neovim as there will be nvim-cmp)
if !has("nvim")
    imap <silent><C-j>      <C-x><C-o>
endif

" Jump to prev/next l(ocation-list), q(uick-fix-list), (dia)g(nostic-items)
nmap         ]l         <cmd>lnext<cr>
nmap         [l         <cmd>lprevious<cr>
nmap         ]q         <cmd>cnext<cr>
nmap         [q         <cmd>cprevious<cr>
nmap <silent>[g         <cmd>lua vim.lsp.diagnostic.goto_prev()<cr>
nmap <silent>]g         <cmd>lua vim.lsp.diagnostic.goto_next()<cr>

" Go to something
nmap <silent>gD         <cmd>lua vim.lsp.buf.declaration()<cr>
nmap <silent>gd         <cmd>lua vim.lsp.buf.definition()<cr>
nmap <silent>gy         <cmd>lua vim.lsp.buf.type_definition()<cr>
nmap <silent>gi         <cmd>lua vim.lsp.buf.implementation()<cr>
nmap <silent>gr         <cmd>lua vim.lsp.buf.references()<cr>

" Inspection
nmap <silent>K          <cmd>lua vim.lsp.buf.hover()<cr>
nmap <silent><c-k>      <cmd>lua vim.lsp.buf.signature_help()<cr>

" Refactoring (and formatting)
nmap <silent><leader>rr <cmd>lua vim.lsp.buf.rename()<cr>
nmap <silent><leader>ra <cmd>lua vim.lsp.buf.code_action()<cr>
nmap <silent><leader>rf <cmd>lua vim.lsp.buf.formatting()<cr>
vmap <silent><leader>rf <cmd>lua vim.lsp.buf.range_formatting()<cr>

" Fuzzy finder
if has('nvim')
    nmap <silent><leader>g  <cmd>Telescope live_grep<cr>
    nmap <silent><c-p>      <cmd>Telescope find_files<cr>
    nmap <silent><leader>ff <cmd>Telescope find_files<cr>
    nmap <silent><leader>fb <cmd>Telescope buffers<cr>
    nmap <silent><leader>fs <cmd>Telescope lsp_document_symbols<cr>
    nmap <silent><leader>fS <cmd>Telescope lsp_workspace_symbols<cr>
    nmap <silent><leader>fd <cmd>Telescope lsp_definitions<cr>
    nmap <silent><leader>fr <cmd>Telescope lsp_references<cr>
    nmap <silent><leader>fi <cmd>Telescope lsp_implementations<cr>
    nmap <silent><leader>fg <cmd>Telescope lsp_document_diagnostics<cr>
    nmap <silent><leader>fG <cmd>Telescope lsp_workspace_diagnostics<cr>
else
    nmap         <c-p>      <cmd>Files<cr>
endif

" Move forward/backward till next non-wsp at same the colum
" https://vi.stackexchange.com/a/693
nmap <silent>gJ     <cmd>call search('\%' . virtcol('.') . 'v\S', 'W')<CR>
nmap <silent>gK     <cmd>call search('\%' . virtcol('.') . 'v\S', 'bW')<CR>

" submode to control split window size (vim-submode)
call submode#enter_with('winsize', 'n', '', '<C-w>>', '<C-w>>')
call submode#enter_with('winsize', 'n', '', '<C-w><', '<C-w><')
call submode#enter_with('winsize', 'n', '', '<C-w>+', '<C-w>+')
call submode#enter_with('winsize', 'n', '', '<C-w>-', '<C-w>-')
call submode#map('winsize', 'n', '', '>', '<C-w>>')
call submode#map('winsize', 'n', '', '<', '<C-w><')
call submode#map('winsize', 'n', '', '+', '<C-w>+')
call submode#map('winsize', 'n', '', '-', '<C-w>-')
" }}}

" Indentation
set shiftround
function! s:SetIndent(size, expand)
    if a:expand == 1
        set   expandtab
    else
        set noexpandtab
    endif
    let &tabstop = a:size
    let &softtabstop = a:size
    let &shiftwidth = a:size
endfunction
call s:SetIndent(4, 1)
augroup indent
    autocmd!
    autocmd FileType gitconfig       call s:SetIndent(8, 0)
    autocmd FileType make            call s:SetIndent(8, 0)
    autocmd FileType go              call s:SetIndent(4, 0)
    autocmd FileType css             call s:SetIndent(2, 1)
    autocmd FileType html            call s:SetIndent(2, 1)
    autocmd FileType javascript      call s:SetIndent(2, 1)
    autocmd FileType javascriptreact call s:SetIndent(2, 1)
    autocmd FileType typescript      call s:SetIndent(2, 1)
    autocmd FileType typescriptreact call s:SetIndent(2, 1)
augroup END

augroup GolangSettings
    autocmd!
    autocmd FileType go nmap <leader>b  <Plug>(go-build)
    autocmd FileType go nmap <leader>r  <Plug>(go-run)
    let g:go_auto_sameids = 1
augroup END

augroup PythonSettings
    autocmd!
    autocmd FileType python set foldmethod=indent
    autocmd FileType python set foldlevel=999
augroup END

augroup RustSettings
    autocmd!
    autocmd FileType rust nmap <leader>b :make build<cr>
    autocmd FileType rust nmap <leader>t :make test<cr>
    let g:rust_cargo_check_tests = 1
augroup END
